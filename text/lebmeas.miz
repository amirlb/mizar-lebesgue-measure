:: Some properties of the 1-dimensional Lebesgue measure
::  by Amir Livne Bar-on
::
:: Copyright (c) 2016 Amir Livne Bar-on
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies FUNCT_1, NUMBERS, SUBSET_1, SUPINF_2, ARYTM_3, CARD_1, RELAT_1,
      TARSKI, ORDINAL2, XXREAL_0, NAT_1, XXREAL_2, ORDINAL1, XBOOLE_0,
      ZFMISC_1, FUNCOP_1, MEASURE5, FUNCT_2, SUPINF_1, MCART_1, MEASURE4,
      REAL_1, PROB_1, MEASURE1, MEASURE7, FUNCT_7, XCMPLX_0, MEMBERED,
      LEBMEAS, VALUED_0, XXREAL_1, ARYTM_1, INT_1, ABSVALUE, COMPLEX1,
      NEWTON, XREAL_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, DOMAIN_1, ORDINAL1,
      NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, XXREAL_3, RELAT_1, FUNCT_1,
      VALUED_0, XXREAL_1, INT_1, COMPLEX1, NEWTON,
      RELSET_1, PARTFUN1, FUNCT_2, FUNCOP_1, NAT_1, PROB_1, XXREAL_2, SUPINF_1,
      SUPINF_2, MEASURE1, MEASURE4, MEASURE5, RECDEF_1, MEASURE7, MEMBERED;
 constructors PARTFUN1, DOMAIN_1, FUNCOP_1, NAT_1, MEASURE4, MEASURE5,
      VALUED_0, RECDEF_1, SUPINF_1, XREAL_0, RELSET_1, XXREAL_2, MEASURE1,
      PROB_2, MEASURE7, XXREAL_1, INT_1, COMPLEX1, ABSVALUE, NEWTON,
      ORDINAL1, NUMBERS, XXREAL_0, ABIAN;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, FUNCT_2, NUMBERS, XREAL_0,
      MEMBERED, MEASURE1, MEASURE4, VALUED_0, XXREAL_2, XXREAL_3, FUNCT_1,
      RELSET_1, XXREAL_0, MEASURE7, MEASURE5, XXREAL_1, INT_1, ABSVALUE,
      NEWTON;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, MEASURE4, XBOOLE_0, XXREAL_2, VALUED_0, MEASURE7,
      MEASURE1, XREAL_0, MEMBERED, SUBSET_1, XXREAL_1, XTUPLE_0, RELAT_1,
      XXREAL_0, INT_1, MEASURE5, NEWTON;
 equalities SUPINF_2, XXREAL_1;
 expansions TARSKI;
 theorems TARSKI, SUPINF_2, MEASURE1, FUNCT_1, MEASURE5, MEASURE6, ZFMISC_1,
      FUNCT_2, NAT_1, XBOOLE_0, XBOOLE_1, FUNCOP_1, XXREAL_0, ORDINAL1,
      XXREAL_2, XXREAL_3, XTUPLE_0, MEASURE7, PROB_1, MEASURE4, XREAL_0,
      SUBSET_1, XREAL_1, VALUED_0, XXREAL_1, NUMBERS, MEMBERED, INT_1,
      ABSVALUE, COMPLEX1, PREPOWER;
 schemes FUNCT_2, NAT_1, BINOP_2, SUBSET_1, XBOOLE_0, XFAMILY;

begin

definition
  let A be Subset of REAL;
  attr A is lebesgue_measurable means :DefMeasurable:
    A in Lmi_sigmaFIELD;
end;

registration
  cluster {} REAL -> lebesgue_measurable;
  coherence proof
    {} in Lmi_sigmaFIELD by PROB_1:4;
    hence {} REAL in Lmi_sigmaFIELD by SUBSET_1:def 2;
  end;
  cluster [#] REAL -> lebesgue_measurable;
  coherence proof
    REAL in Lmi_sigmaFIELD by PROB_1:5;
    hence [#] REAL in Lmi_sigmaFIELD by SUBSET_1:def 3;
  end;
  cluster lebesgue_measurable for Subset of REAL;
  existence proof
    take [#] REAL;
    thus thesis;
  end;
end;

definition
  let a, b be Real;
  redefine func [.a, b.] -> Subset of REAL;
  coherence
  proof
    a in REAL & b in REAL by XREAL_0:def 1;
    hence thesis by XXREAL_1:228;
  end;
end;

theorem ThThinIsMeasurable:
  for A being Subset of REAL st OS_Meas.A = 0 holds A is lebesgue_measurable
  by MEASURE4:15, MEASURE7:def 12;

theorem ThCompat:
  for A being lebesgue_measurable Subset of REAL holds L_mi.A = OS_Meas.A
proof
  let A be lebesgue_measurable Subset of REAL;
  Lmi_sigmaFIELD = sigma_Field(OS_Meas) by MEASURE7:def 12;
  then L_mi.A = OS_Meas.A by DefMeasurable, MEASURE4:def 3, MEASURE7:def 13;
  hence thesis;
end;

theorem ThEmptySetIsThin:
  OS_Meas.{} = 0
proof
  OS_Meas is zeroed by MEASURE4:def 1;
  hence thesis by VALUED_0:def 19;
end;

registration
  cluster empty -> lebesgue_measurable for Subset of REAL;
  coherence by ThThinIsMeasurable, ThEmptySetIsThin;
end;

theorem ThEmptySetMeasureZero:
  L_mi.{} = 0 by VALUED_0:def 19;

theorem ThElementOfSequenceUnion:
  for X being set
  for S being sequence of bool X
  for x being Element of X
  holds x in union (rng S) iff ex n being Element of NAT st x in S.n
proof
  let X be set;
  let S be sequence of bool X;
  let x be Element of X;
  hereby
    assume x in union (rng S);
    then consider A being set such that
      A1: x in A & A in rng S by TARSKI:def 4;
    consider n being object such that
      A2: n in dom S & A = S.n by A1, FUNCT_1:def 3;
    reconsider n as Element of NAT by A2;
    take n;
    thus x in S.n by A1, A2;
  end;
  hereby
    assume ex n being Element of NAT st x in S.n;
    then consider n being Element of NAT such that A3: x in S.n;
    S.n in rng S by FUNCT_2:4;
    hence x in union (rng S) by A3, TARSKI:def 4;
  end;
end;

theorem ThSingletonIsThin:
  for x being Element of REAL holds OS_Meas.({x}) = 0
proof
  let x be Element of REAL;
  defpred P[object, object] means
    (($1 = 0 & $2 = {x}) or ($1 <> 0 & $2 = {}REAL));
  A1: for n being Element of NAT holds
      ex xs being Element of bool REAL st P[n,xs]
  proof
    let n be Element of NAT;
    per cases;
    suppose A2: n = 0;
      take xs = {x};
      thus thesis by A2;
    end;
    suppose A3: n <> 0;
      take xs = {}REAL;
      thus thesis by A3;
    end;
  end;
  consider F being Function of NAT, bool REAL such that
  A4: for n being Element of NAT holds P[n, F.n] from FUNCT_2:sch 3(A1);
  A5: for n being Element of NAT holds F.n is Interval
  proof
    let n be Element of NAT;
    per cases;
    suppose n = 0;
      then F.n = {x} = [.x,x.] by A4, XXREAL_1:17;
      hence thesis;
    end;
    suppose n <> 0;
      hence thesis by A4;
    end;
  end;
  A6: for n being Element of NAT holds diameter(F.n) = 0
  proof
    reconsider y = x as R_eal by NUMBERS:31;
    let n be Element of NAT;
    per cases;
    suppose n = 0;
      then F.n = {y} = [.y,y.] by A4, XXREAL_1:17;
      then diameter(F.n) = x - x by MEASURE5:6;
      hence thesis;
    end;
    suppose n <> 0;
      hence thesis by A4, MEASURE5:10;
    end;
  end;
  F.0 = {x} by A4;
  then {x} in rng F by FUNCT_2:4;
  then A7: {x} c= union (rng F) by ZFMISC_1:74;
  reconsider G = F as Interval_Covering of {x} by MEASURE7:def 2, A5, A7;
  now
    let n be Element of NAT;
    (G vol).n = diameter(F.n) by MEASURE7:def 4;
    hence (G vol).n = 0 by A6;
  end;
  then SUM(G vol) = 0 by MEASURE7:1;
  then vol(G) = 0 by MEASURE7:def 6;
  then 0 in Svc({x}) by MEASURE7:def 8;
  then inf Svc({x}) <= 0 by XXREAL_2:3;
  then A8: OS_Meas.({x}) <= 0 by MEASURE7:def 10;
  OS_Meas is nonnegative by MEASURE4:def 1;
  hence thesis by A8, MEASURE1:def 2;
end;

theorem ThSingletonIsMeasurable:
  for x being Element of REAL holds {x} is lebesgue_measurable
proof
  let x be Element of REAL;
  OS_Meas.({x}) = 0 by ThSingletonIsThin;
  hence thesis by ThThinIsMeasurable;
end;

theorem ThSingletonMeasureZero:
  for x being Real holds L_mi.({x}) = 0
proof
  let x be Real;
  reconsider xx = x as Element of REAL by XREAL_0:def 1;
  A1: {xx} is lebesgue_measurable by ThSingletonIsMeasurable;
  hence L_mi.({x})
      = L_mi.({xx})
     .= OS_Meas.({xx}) by A1, ThCompat
     .= 0 by ThSingletonIsThin;
end;

registration
  cluster nonnegative for sequence of ExtREAL;
  existence
  proof
    reconsider nil = 0 as R_eal by XREAL_0:def 1, NUMBERS:31;
    deffunc F(Element of NAT) = nil;
    consider S being sequence of ExtREAL such that
      A1: for n being Element of NAT holds S.n = F(n) from FUNCT_2:sch 4;
    take S;
    for n being Element of NAT holds S.n >= 0 by A1;
    hence thesis by A1, SUPINF_2:39;
  end;
end;

theorem ThSumNonnegative:
  for F being nonnegative sequence of ExtREAL holds SUM(F) >= 0
proof
  let F be nonnegative sequence of ExtREAL;
  set a = (Ser F).0;
  (Ser F).0 = F.0 by SUPINF_2:def 11;
  then (Ser F).0 in rng F by FUNCT_2:4;
  then A1: (Ser F).0 >= 0 by SUPINF_2:def 12, SUPINF_2:def 9;
  (Ser F).0 in rng (Ser F) by FUNCT_2:4;
  then sup (rng (Ser F)) >= 0 by A1, XXREAL_2:4;
  hence thesis by SUPINF_2:def 13;
end;

registration
  let A be Subset of REAL;
  let F be Interval_Covering of A;
  cluster F vol -> nonnegative;
  coherence
  proof
    now
      let n be Element of NAT;
      0 <= diameter(F.n) by MEASURE5:13;
      hence 0 <= (F vol).n by MEASURE7:def 4;
    end;
    hence F vol is nonnegative by SUPINF_2:39;
  end;
end;

theorem ThVolIsNonnegative:
  for A being Subset of REAL
  for F being Interval_Covering of A
  holds vol(F) >= 0
proof
  let A be Subset of REAL;
  let F be Interval_Covering of A;
  SUM(F vol) >= 0 by ThSumNonnegative;
  hence thesis by MEASURE7:def 6;
end;

theorem ThTransitiveInequality:
  for a, b, c being ExtReal st a <= b & b < c holds a < c
proof
  let a, b, c be ExtReal;
  assume that A1: a <= b
  and A2: b < c;
  per cases;
  suppose a = b;
    hence thesis by A2;
  end;
  suppose a <> b;
    then a < b by A1, XXREAL_0:1;
    hence thesis by A2, XXREAL_0:2;
  end;
end;

theorem ThArchimedeanProperty:
  for x being Real ex n being Nat st -n < x & x < n
proof
  let x be Real;
  set y = |. x .|;
  A1: -y <= x & x <= y by ABSVALUE:4;
  set n = [\ y /] + 1;
  A2: y < n by INT_1:29;
  then n >= 0 by COMPLEX1:46;
  then reconsider n as Nat by INT_1:3, ORDINAL1:def 12;
  take n;
  -n < -y & y < n by A2, XXREAL_3:38;
  hence -n < x & x < n by A1, XXREAL_0:2;
end;

definition
  let A be Subset of REAL;
  mode Open_Interval_Covering of A -> Interval_Covering of A means :Def2:
    for n being Element of NAT holds it.n is open_interval;
  existence
  proof
    A1: NAT c= ExtREAL by NUMBERS:19, NUMBERS:31;
    defpred P[Element of NAT, object] means ($2 = ]. -$1, $1 .[);
    A2: for n being Element of NAT holds
        ex xs being Element of bool REAL st P[n,xs]
    proof
      let n be Element of NAT;
      reconsider n as R_eal by A1;
      take ]. -n, n .[;
      thus thesis;
    end;
    consider F being Function of NAT, bool REAL such that
    A3: for n being Element of NAT holds P[n, F.n] from FUNCT_2:sch 3(A2);
    A4: for n being Element of NAT holds F.n is open_interval
    proof
      let n be Element of NAT;
      reconsider n as R_eal by A1;
      F.n = ]. -n, n .[ by A3;
      hence thesis by MEASURE5:def 2;
    end;
    for I being Subset of REAL holds I is open_interval implies I is Interval;
    then A5: for n being Element of NAT holds F.n is Interval by A4;
    A c= union (rng F)
    proof
      now
        let x be Element of A;
        consider n being Nat such that A6: -n < x & x < n by ThArchimedeanProperty;
        reconsider n as Element of NAT by ORDINAL1:def 12;
        x in ].-n, n.[ by A6, XXREAL_1:4;
        then x in F.n by A3;
        hence x in union (rng F) by ThElementOfSequenceUnion;
      end;
      hence thesis;
    end;
    then reconsider F as Interval_Covering of A by A5, MEASURE7:def 2;
    take F;
    thus thesis by A4;
  end;
end;

definition
  let I be Interval;
  let h be Real;
  func expand_interval(I, h) -> open_interval Subset of REAL equals :Def4:
    {} if I = {} or h < 0
    otherwise ]. inf I - h/2, sup I + h/2 .[;
  consistency;
  coherence
  proof
    hereby
      assume (I = {} or h < 0);
      thus {} is open_interval Subset of REAL by SUBSET_1:1;
    end;
    assume not (I = {} or h < 0);
    reconsider d = h/2 as R_eal by NUMBERS:31, XREAL_0:def 1;
    reconsider J = ]. inf I - d, sup I + d .[ as Subset of REAL
      by XXREAL_1:225;
    J is open_interval by MEASURE5:def 2;
    hence thesis;
  end;
end;

theorem ThInfSupOfRealSet:
  for A being Subset of REAL st A <> {}
  holds inf A <> +infty & sup A <> -infty
proof
  let A be Subset of REAL;
  assume A <> {};
  then consider x being object such that A1: x in A by XBOOLE_0:7;
  reconsider x as Element of REAL by A1;
  per cases;
  suppose inf A = +infty;
    then +infty is LowerBound of A by XXREAL_2:def 4;
    then +infty <= x by A1, XXREAL_2:def 2;
    then contradiction by XXREAL_0:9;
    hence thesis;
  end;
  suppose sup A = -infty;
    then -infty is UpperBound of A by XXREAL_2:def 3;
    then -infty >= x by A1, XXREAL_2:def 1;
    then contradiction by XXREAL_0:12;
    hence thesis;
  end;
  suppose inf A <> +infty & sup A <> -infty;
    hence thesis;
  end;
end;

theorem ThExpansionContainsOrig:
  for I being Interval
  for h being Real st h > 0
  holds I c= expand_interval(I, h)
proof
  let I be Interval;
  let h be Real;
  per cases;
  suppose I = {};
    hence thesis;
  end;
  suppose A1: I <> {};
    assume A2: h > 0;
    now
      let x0 be set;
      assume A3: x0 in I;
      then reconsider x = x0 as Real;
      A4: inf I - h/2 < x
      proof
        per cases by XXREAL_0:14;
        suppose inf I = +infty;
          then +infty is LowerBound of I by XXREAL_2:def 4;
          then I c= {+infty} by XXREAL_2:52;
          then x = +infty by A3, TARSKI:def 1;
          hence thesis by A3, XXREAL_0:8;
        end;
        suppose inf I = -infty;
          then -infty = inf I + -(h/2) by XXREAL_3:def 2
                     .= inf I - h/2 by XXREAL_3:def 4;
          hence thesis by A3, XXREAL_0:12;
        end;
        suppose inf I in REAL;
          then reconsider i = inf I as Real;
          A5: i <= x by A3, XXREAL_2:3;
          h/2 > 0 by A2, XREAL_1:215;
          then i - h/2 < x - 0 = x by A5, XREAL_1:15;
          hence thesis;
        end;
      end;
      A6: x < sup I + h/2
      proof
        per cases by XXREAL_0:14;
        suppose sup I = +infty;
          then +infty = sup I + h/2 by XXREAL_3:def 2;
          hence thesis by A3, XXREAL_0:9;
        end;
        suppose sup I = -infty;
          then -infty is UpperBound of I by XXREAL_2:def 3;
          then I c= {-infty} by XXREAL_2:51;
          then x = -infty by A3, TARSKI:def 1;
          hence thesis by A3, XXREAL_0:11;
        end;
        suppose sup I in REAL;
          then reconsider s = sup I as Real;
          A7: x <= s by A3, XXREAL_2:4;
          h/2 > 0 by A2, XREAL_1:215;
          then x < s + h/2 by A7, XREAL_1:39;
          hence thesis;
        end;
      end;
      x in ]. inf I - h/2, sup I + h/2 .[ by A4, A6, XXREAL_1:4;
      then x in expand_interval(I, h) by A1, A2, Def4;
      hence x0 in expand_interval(I, h);
    end;
    hence thesis;
  end;
end;

theorem ThExpansionIsLonger:
  for I being Interval
  for h being Real st h > 0
  holds diameter(expand_interval(I, h)) <= diameter(I) + h
proof
  let I be Interval;
  let h be Real;
  assume A1: h > 0;
  set J = expand_interval(I, h);
  per cases;
  suppose A2: I = {};
    then A3: diameter(I) = 0 by MEASURE5:def 6;
    J = {} by A2, Def4;
    then diameter(J) = 0 by MEASURE5:def 6;
    hence diameter(J) <= diameter(I) + h by A1, A3;
  end;
  suppose A4: I <> {};
    A5: diameter(I) = sup I - inf I by A4, MEASURE5:def 6;
    reconsider d = h/2 as R_eal by XREAL_0:def 1, NUMBERS:31;
    A6: J = ]. inf I - d, sup I + d .[ by A1, A4, Def4;
    A7: inf I - d < sup I + d
    proof
      assume sup I + d <= inf I - d;
      then J = {} by A6, XXREAL_1:28;
      then I c= {} by A1, ThExpansionContainsOrig;
      hence contradiction by A4;
    end;
    A8: inf I <> +infty & sup I <> -infty & sup I + d <> -infty
      by A4, A7, ThInfSupOfRealSet, XXREAL_0:5;
    A9: diameter(J) = (sup I + d) - (inf I - d) by A6, A7, MEASURE5:5
                   .= sup I + d - inf I + d by A8, XXREAL_3:32
                   .= sup I - inf I + d + d by A8, XXREAL_3:30
                   .= sup I - inf I + (d + d) by A8, XXREAL_3:29;
    thus diameter(J) <= diameter(I) + h by A1, A5, A9, XXREAL_3:105;
  end;
end;

theorem ThSumSubadditive:
  for S1, S2, S3 being nonnegative sequence of ExtREAL
  st (for n being Element of NAT holds S1.n <= S2.n + S3.n)
  holds SUM(S1) <= SUM(S2) + SUM(S3)
proof
  let S1, S2, S3 be nonnegative sequence of ExtREAL;
  assume A1: for n being Element of NAT holds S1.n <= S2.n + S3.n;
  deffunc F(Element of NAT) = S2.$1 + S3.$1;
  consider S4 being Function of NAT, ExtREAL such that
    A2: for n being Element of NAT holds S4.n = F(n) from FUNCT_2:sch 4;
  now
    let n be Element of NAT;
    S4.n = S2.n + S3.n by A2;
    hence S1.n <= S4.n by A1;
  end;
  then A3: SUM(S1) <= SUM(S4) by SUPINF_2:43;
  for n being Element of NAT holds S4.n = S2.n + S3.n by A2;
  then SUM(S4) <= SUM(S2) + SUM(S3) by MEASURE7:4;
  hence thesis by A3, XXREAL_0:2;
end;

theorem ThExtendToOpenIntervals:
  for A being Subset of REAL
  for F being Interval_Covering of A
  for h being Real st h > 0
  ex G being Open_Interval_Covering of A
  st vol(G) <= vol(F) + h
proof
  let A be Subset of REAL;
  let F be Interval_Covering of A;
  let h be Real;
  assume A1: h > 0;
  consider S being sequence of ExtREAL such that
    A2: (for n being Nat holds S.n > 0) & SUM(S) < h by A1, MEASURE6:4;
  A3: S is nonnegative
  proof
    now
      let n be Element of NAT;
      thus S.n >= 0 by A2;
    end;
    hence thesis by SUPINF_2:39;
  end;
  rng S c= REAL
  proof
    now
      let x be Element of (rng S);
      A4: ex n being object st n in dom S & x = S.n by FUNCT_1:def 3;
      h < +infty by XREAL_0:def 1, XXREAL_0:9;
      then SUM(S) < +infty by A2, XXREAL_0:2;
      hence x in REAL by A3, A4, MEASURE6:7;
    end;
    hence thesis by SUBSET_1:27;
  end;
  then reconsider R = S as Function of dom(S), REAL by FUNCT_2:2;
  deffunc I(Element of NAT) = expand_interval(F.$1, R.$1);
  consider G being Function of NAT, bool REAL such that
    A5: for n being Element of NAT holds G.n = I(n) from FUNCT_2:sch 4;
  A6: A c= union (rng F) c= union (rng G)
  proof
    thus A c= union (rng F) by MEASURE7:def 2;
    now
      let x be set;
      assume x in union (rng F);
      then consider n being Element of NAT such that
        A7: x in F.n by ThElementOfSequenceUnion;
      S.n > 0 by A2;
      then F.n c= I(n) by A1, ThExpansionContainsOrig;
      then x in I(n) by A7;
      then x in G.n by A5;
      hence x in union (rng G) by ThElementOfSequenceUnion;
    end;
    hence union (rng F) c= union (rng G);
  end;
  for n being Element of NAT holds G.n is Interval
  proof
    let n be Element of NAT;
    G.n = I(n) by A5;
    hence thesis;
  end;
  then reconsider G as Interval_Covering of A
    by A6, MEASURE7:def 2, XBOOLE_1:1;
  for n being Element of NAT holds G.n is open_interval
  proof
    let n be Element of NAT;
    G.n = I(n) by A5;
    hence thesis;
  end;
  then reconsider G as Open_Interval_Covering of A by Def2;
  take G;
  A7: vol(F) = SUM(F vol) & vol(G) = SUM(G vol) by MEASURE7:def 6;
  now
    let n be Element of NAT;
    A8: R.n > 0 by A2;
    A9: (F vol).n = diameter(F.n) & (G vol).n = diameter(G.n) by MEASURE7:def 4;
    G.n = expand_interval(F.n, R.n) by A5;
    hence (G vol).n <= (F vol).n + S.n by A8, A9, ThExpansionIsLonger;
  end;
  then A10: vol(G) <= vol(F) + SUM(S) by A7, A3, ThSumSubadditive;
  SUM(S) < h by A2;
  then vol(F) + SUM(S) <= vol(F) + h by XXREAL_3:35;
  hence vol(G) <= vol(F) + h by A10, XXREAL_0:2;
end;

theorem ThQuantitativeHeineBorel:
  for a, b being Real st a <= b
  for F being Open_Interval_Covering of [.a, b.]
  holds vol(F) >= b - a;
::>                   *4

theorem ThClosedIntervalOuterMeasure:
  for a, b being Real st a <= b holds OS_Meas.([.a, b.]) = b - a
proof
  let a, b be Real;
  assume A1: a <= b;
  reconsider I = [.a, b.] as Subset of REAL;
  A2: b - a in Svc(I)
  proof
    deffunc C() = NAT;
    deffunc D() = bool REAL;
    defpred P[object, object] means
      (($1 = 0 & $2 = I) or ($1 <> 0 & $2 = {}REAL));
    A3: for n being Element of NAT holds
        ex xs being Element of bool REAL st P[n,xs]
    proof
      let n be Element of NAT;
      per cases;
      suppose Cond: n = 0;
        take xs = I;
        thus thesis by Cond;
      end;
      suppose Cond: n <> 0;
        take xs = {}REAL;
        thus thesis by Cond;
      end;
    end;
    consider F being Function of NAT, bool REAL such that
    A4: for n being Element of NAT holds P[n, F.n] from FUNCT_2:sch 3(A3);
    A5: for n being Element of NAT holds F.n is Interval by A4;
    F.0 = I by A4;
    then I in rng F by FUNCT_2:4;
    then I c= union (rng F) by ZFMISC_1:74;
    then reconsider G = F as Interval_Covering of I by MEASURE7:def 2, A5;
    reconsider H = G vol as sequence of ExtREAL;
    now
      let n be Element of NAT;
      assume 1 <= n;
      then G.n = {} by A4;
      hence H.n = 0 by MEASURE7:def 4, MEASURE5:10;
    end;
    then A6: SUM(H)
               = Ser(H).1
               = Ser(H).(0+1)
               = Ser(H).0 + H.1
               = H.0 + H.1 by MEASURE7:12, SUPINF_2:48, SUPINF_2:def 11;
    now
      reconsider a1 = a as R_eal by XREAL_0:def 1, NUMBERS:31;
      reconsider b1 = b as R_eal by XREAL_0:def 1, NUMBERS:31;
      H.0 = diameter(G.0) = diameter(I) by A4, MEASURE7:def 4;
      then A7: H.0 = b1 - a1 by A1, MEASURE5:6;
      G.1 = {} by A4;
      then H.1 = 0 by MEASURE7:def 4, MEASURE5:10;
      then H.0 + H.1 = (b1 - a1) + 0 = (b - a) + 0 by A7;
      hence H.0 + H.1 = b - a;
    end;
    then vol(G) = b - a by A6, MEASURE7:def 6;
    hence thesis by MEASURE7:def 8;
  end;
  b - a is LowerBound of Svc(I)
  proof
    let x be ExtReal;
    assume x in Svc(I);
    then consider F being Interval_Covering of I such that
      A9: x = vol(F) by MEASURE7:def 8;
    assume A10: x < b - a;
    x in REAL
    proof
      per cases by XXREAL_0:14;
      suppose x in REAL;
        hence thesis;
      end;
      suppose x = +infty;
        then contradiction by XREAL_0:def 1, A10, XXREAL_0:9;
        hence thesis;
      end;
      suppose x = -infty;
        then contradiction by A9, ThVolIsNonnegative, XXREAL_0:12;
        hence thesis;
      end;
    end;
    then reconsider x as Real;
    set d = ((b - a) - x) / 2;
    (b - a) - x > 0 by A10, XREAL_1:50;
    then d > 0 by XREAL_1:139;
    then consider G being Open_Interval_Covering of I such that
      A11: vol(G) <= x + d by A9, ThExtendToOpenIntervals;
    x + (b-a) < (b-a) + (b-a) by A10, XREAL_1:6;
    then (x + (b - a)) / 2 < ((b-a) + (b-a)) / 2 by XREAL_1:74;
    then x + d < b - a by A10, XREAL_1:74;
    then vol(G) < b - a by A11, ThTransitiveInequality;
    hence contradiction by A1, ThQuantitativeHeineBorel;
  end;
  then inf(Svc(I)) = b - a by A2, XXREAL_2:56;
  hence thesis by MEASURE7:def 10;
end;
::> 4: This inference is not accepted
